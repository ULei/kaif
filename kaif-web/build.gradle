buildscript {
  ext {
    springBootVersion = '1.2.8.RELEASE'
    // spring boot now use 8.0.30
    tomcatVersion = '8.0.30'
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
    classpath "com.github.houbie:lesscss-gradle-plugin:1.0.3-less-1.7.0"

    // enable advance class hot swap. don't know if work
    // classpath 'org.springframework:springloaded:1.2.0.RELEASE'
  }
}

def appBuild = 'build-' + new Date().format('yyyy-MM-dd-HH-mm-ss')

apply plugin: 'war'

/** spring boot begin **/
apply plugin: 'spring-boot'

bootRepackage {
  mainClass = 'io.kaif.Application'
}

springBoot {
  mainClass = 'io.kaif.Application'
}

// hack to make `gradle bootRun` start with dev mode
tasks.withType(org.springframework.boot.gradle.run.BootRunTask) {
  systemProperty('spring.profiles.active', 'dev')
}

/** spring boot end **/

/** dart configuration begin **/

def dartDist = "dart_dist"

def getDartPub() {
  def envDart = System.env['dart_sdk_home']
  def propDart = System.properties['dart_sdk_home']
  if (envDart == null && propDart == null) {
    return null;
  }
  if (envDart != null) {
    return "${System.env['dart_sdk_home']}/bin/pub"
  }
  return "${System.properties['dart_sdk_home']}/bin/pub"
}

task buildDart(dependsOn: classes, type: Exec) {
  doFirst {
    if (getDartPub() == null) {
      throw new IllegalStateException("missing 'dart_sdk_home' environment var., please install dart and set env first");
    }
  }
  workingDir "src/main/dart"
  commandLine getDartPub(), "build", "-o", "${buildDir}/${dartDist}"
}

task pubServe(type: Exec) {
  doFirst {
    if (getDartPub() == null) {
      throw new IllegalStateException("missing 'dart_sdk_home' environment var., please install dart and set env first");
    }
  }
  workingDir "src/main/dart"
  commandLine getDartPub(), "serve", "--port", "15980", "--hostname", "localhost", "--force-poll"
}

war.dependsOn buildDart

/** dart configuration end **/

/** less configuration begin **/

apply plugin: "lesscss"

def lessDist = "less_dist"

lessc {
  sourceDir "$webAppDirName/less"
  include "kaif.less"
  destinationDir = "$buildDir/$lessDist"
}

war.dependsOn lessc

/** less configuration end **/

import org.apache.tools.ant.filters.*

// replace @app.build@ in application-prod.yml
processResources {
  filesMatching('**/application-prod.yml') {
    filter ReplaceTokens, tokens: [
        "app.build": appBuild
    ]
  }
}

jar {
  baseName = 'kaif-web'
}

war {
  baseName = 'kaif-web'
  from("${buildDir}/$dartDist") {
    into "${appBuild}"
  }
  from("${buildDir}/$lessDist") {
    into "${appBuild}/css"
  }
  from("${webAppDirName}/snapshot") {
    into "${appBuild}"
  }
  exclude "snapshot"
  exclude "less"
}

dependencies {
  compile("org.springframework.boot:spring-boot-starter-aop")

  compile("org.springframework.boot:spring-boot-starter-jdbc") {
    // uncomment if you want to remove tomcat jdbc pool
    exclude group: 'org.apache.tomcat';
  }
  compile("org.springframework.boot:spring-boot-starter-freemarker")
  compile("org.springframework.boot:spring-boot-starter-web") {
    exclude module: 'spring-boot-starter-tomcat'
  }

  //exclude embed tomcat in production
  providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")
  providedRuntime "org.apache.tomcat:tomcat-jdbc:${tomcatVersion}"

  // compile("org.springframework.boot:spring-boot-starter-social-twitter")
  compile("org.springframework.boot:spring-boot-starter-actuator")
  compile("org.springframework.boot:spring-boot-starter-remote-shell")
  testCompile("org.springframework.boot:spring-boot-starter-test")

  //upgrade spring should bound json-path version too
  testCompile('com.jayway.jsonpath:json-path:1.2.0')

  compile 'org.springframework.security:spring-security-crypto:3.2.5.RELEASE';

  compile('org.postgresql:postgresql:9.4.1208') {
    exclude group: 'org.slf4j'
  }

  compile 'com.google.guava:guava:19.0'

  // postgresql driver not better fit HikariCP
  // compile 'com.zaxxer:HikariCP:2.3.0'

  // note that guava depends on jsr305 1.3.8
  compile 'com.google.code.findbugs:jsr305:3.0.0'
  compile 'javax.inject:javax.inject:1'

  compile 'com.fasterxml.jackson.module:jackson-module-afterburner:2.4.5'
  compile 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310:2.4.5'

  // webjars
  compile 'org.webjars:font-awesome:4.2.0'
  compile 'org.webjars:yui-pure:0.5.0'

  // we are use old less 1.7.0 because lesscss-gradle-plugin use old one
  //
  // @see buildscript - lesscss-gradle-plugin
  // @see webapp/WEB-INF/views/macros/template.ftl
  compile 'org.webjars:less:1.7.0'

  // aws will depends on jackson/joda/httpclient
  compile 'com.amazonaws:aws-java-sdk-ses:1.9.16'

  compile 'org.ocpsoft.prettytime:prettytime:3.2.7.Final'
  compile 'org.apache.commons:commons-math3:3.4.1'

  // rss
  compile 'com.rometools:rome:1.5.0'

  // swagger
  compile "com.mangofactory:swagger-springmvc:1.0.2"
  compile "org.ajar:swagger-spring-mvc-ui:0.4"

  // workaround swagger ui 0.4 bug
  // https://github.com/adrianbk/swagger-springmvc-demo/issues/15
  // this only affect local development with embed tomcat
  providedRuntime "org.apache.tomcat.embed:tomcat-embed-jasper:${tomcatVersion}"
}


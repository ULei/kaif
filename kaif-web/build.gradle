buildscript {
  ext {
    springBootVersion = '1.2.1.RELEASE'
  }
  dependencies {
    classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")

    // enable advance class hot swap. don't know if work
    // classpath 'org.springframework:springloaded:1.2.0.RELEASE'
  }
}

def appBuild = 'build-' + new Date().format('yyyy-MM-dd-HH-mm-ss')

apply plugin: 'war'

/** spring boot begin **/
apply plugin: 'spring-boot'

bootRepackage {
  mainClass = 'io.kaif.Application'
}

springBoot {
  mainClass = 'io.kaif.Application'
}

// hack to make `gradle bootRun` start with dev mode
tasks.withType(org.springframework.boot.gradle.run.BootRunTask) {
  systemProperty('spring.profiles.active', 'dev')
}

/** spring boot end **/

/** dart configuration begin **/

def dartDist = "dart_dist"

def getDartPub() {
  def envDart = System.env['DART_SDK']
  def propDart = System.properties['DART_SDK']
  if (envDart == null && propDart == null) {
    return null;
  }
  if (envDart != null) {
    return "${System.env['DART_SDK']}/bin/pub"
  }
  return "${System.properties['DART_SDK']}/bin/pub"
}

task buildDart(dependsOn: classes, type: Exec) {
  doFirst {
    if (getDartPub() == null) {
      throw new IllegalStateException('missing DART_SDK environment var., please install dart and set env first');
    }
  }
  workingDir "src/main/dart"
  commandLine getDartPub(), "build", "-o", "${buildDir}/${dartDist}"
}

task pubServe(type: Exec) {
  doFirst {
    if (getDartPub() == null) {
      throw new IllegalStateException('missing DART_SDK environment var., please install dart and set env first');
    }
  }
  workingDir "src/main/dart"
  commandLine getDartPub(), "serve", "--port", "15980", "--hostname", "localhost", "--force-poll"
}

war.dependsOn buildDart

/** dart configuration end **/

import org.apache.tools.ant.filters.*

// replace @app.build@ in application-prod.yml
processResources {
  filesMatching('**/application-prod.yml') {
    filter ReplaceTokens, tokens: [
        "app.build": appBuild
    ]
  }
}

jar {
  baseName = 'kaif-web'
}

war {
  baseName = 'kaif-web'
  from("${buildDir}/$dartDist") {
    into "${appBuild}/"
  }
  from("${webAppDirName}/snapshot") {
    into "${appBuild}"
  }
}

dependencies {
  // compile("org.springframework.boot:spring-boot-starter-data-elasticsearch")
  // compile("org.springframework.boot:spring-boot-starter-websocket")

  compile("org.springframework.boot:spring-boot-starter-aop")

  compile("org.springframework.boot:spring-boot-starter-jdbc") {
    // uncomment if you want to remove tomcat jdbc pool
    exclude group: 'org.apache.tomcat';
  }
  compile("org.springframework.boot:spring-boot-starter-freemarker")
  compile("org.springframework.boot:spring-boot-starter-web") {
    exclude module: 'spring-boot-starter-tomcat'
  }

  //exclude embed tomcat in production
  providedRuntime("org.springframework.boot:spring-boot-starter-tomcat")
  // spring boot now use 8.0.15
  providedRuntime 'org.apache.tomcat:tomcat-jdbc:8.0.15'

  // compile("org.springframework.boot:spring-boot-starter-social-twitter")
  compile("org.springframework.boot:spring-boot-starter-actuator")
  compile("org.springframework.boot:spring-boot-starter-remote-shell")
  testCompile("org.springframework.boot:spring-boot-starter-test")

  compile 'org.springframework.security:spring-security-crypto:3.2.5.RELEASE';

  compile 'org.postgresql:postgresql:9.4-1200-jdbc41'
  compile 'com.google.guava:guava:18.0'

  // postgresql driver not better fit HikariCP
  // compile 'com.zaxxer:HikariCP:2.3.0'

  // note that guava depends on jsr305 1.3.8
  compile 'com.google.code.findbugs:jsr305:3.0.0'
  compile 'javax.inject:javax.inject:1'

  compile 'com.fasterxml.jackson.module:jackson-module-afterburner:2.4.0'

  // webjars
  compile 'org.webjars:font-awesome:4.2.0'
  compile 'org.webjars:yui-pure:0.5.0'

  // aws will depends on jackson/joda/httpclient
  compile 'com.amazonaws:aws-java-sdk-ses:1.9.16'
}

